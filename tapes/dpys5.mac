/ dpys5: pdp-1 display hacks
/  051209 - 060525 - 080415

/ start at 0 for munching squares
/ (uses TW)

0/	opr
	opr
	opr
	opr
	jmp msq

/ start at 10 for snowflake
/ (uses no switches)

10/

beg,	law t1a
	dac p1
	law t2a
	dac p2
	
b1,	jda cxy
	dac i p1
	dio i p2
	idx p1
	idx p2
	sas (t2e
	jmp b1
	law i 40
	dac c1
	
b2,	lac i p1
	dac t1
	lac i p2
	dac t2
	
b3,	law i 1
	add p1
	dac p1
	law i 1
	add p2
	dac p2
	sad (t2a
	jmp b4
	jda dxy
	dac i p1
	dio i p2
	jmp b3

b4,	lac i p1
	dac t1
	lac i p2
	dac t2
	
b5,	idx p1
	idx p2
	sad (t2e-1
	jmp b6
	jda dxy
	dac i p1
	dio i p2
	jmp b5

b6,	isp c1
	jmp b8
	jda cxy
	dac t1a
	dio t2a
	jda cxy
	dac t1e-1
	dio t2e-1
	
b7,	jda cxy
	dac t1m
	dio t2m
	jmp b9

b8,	lac x
	dac t1m
	lac y
	dac t2m
	jmp b2

b9,	and (74
	cma
	dac c1
	jmp b2

cxx,	0

cxy,	0
	dap cxx
	lac x
	mul (362643
	rir 1s
	dio x
	lac y
	mul (362643
	rir 1s
	dio y
	add x
	dac x
	rcl 4s
	jmp i cxx

dxx,	0

dxy,	0
	dap dxx
	szo
	cla
	lac i p2
	add t2
	szo
	jmp dx1
	sar 1s
	jmp dx2

dx1,	xor (377777
	sar 1s
	xor (577777
	
dx2,	dac t2
	lac i p1
	add t1
	szo
	jmp dx3
	sar 1s
	jmp dx4

dx3,	xor (377777
	sar 1s
	xor (577777
	
dx4,	dac t1
	lio t2

dy1,	dac z1
	dio z2
	sar 1s
	dac t3
	lac z2
	sar 1s
	dac t4
	lac z1
	mul (335546
	dac q1
	lac z2
	mul (335546
	dac q2
	lac q1
	sub t4
	dac r1
	cma
	dac r2
	lac q1
	add t4
	dac r3
	cma
	dac r4
	lac q2
	sub t3
	lio r3
	
dy2,	dpy 300
	cma
	dpy 300
	cma
	lio r4
	dpy 300
	cma
	dpy 300
	lac t3
	add q2
	lio r1
	dpy 300
	cma
	dpy 300
	lio r2
	dpy 300
	cma
	dpy 300
	lac z2
	cma
	rcl 9s
	rcl 9s
	lac z1
	dpy 300
	cma
	dpy 300
	lio z2
	dpy 300
	cma
	dpy 300
	lac t1
	lio t2
	jmp i dxx

x,	520063

y,	350735

p1,	0

p2,	0

c1,	0

t1,	0

t2,	0

t3,	0

t4,	0

z1,	0

z2,	0

q1,	0

q2,	0

r1,	0

r2,	0

r3,	0

r4,	0

consta

t1a,

.+23/	t1m,

.+45/	t1e,

t2a,
.+33/	t2m,

.+35/	t2e,

471/
ltw,	0
	lat
	sas ltc
	sza i
	law 6543
	jmp i ltw
ltc,	777777

/ start at 500 for Minskytron
/ (uses TW)

500/	lat
	rcl 9s
	rcl 9s
m1,	jsp gsh
	dac sh0
	jsp gsh
	dac sh1
	jsp gsh
	dac sh2
	jsp gsh
	dac sh3
	jsp gsh
	dac sh4
	jsp gsh
	dac sh5
m2,	lac xa0
	dac xa
	lac xb0
	dac xb
	lac xc0
	dac xc
	lac ya0
	dac ya
	lac yb0
	dac yb
	lac yc0
	dac yc

m3a,	lac xa
	add xb
	xct sh0
	add ya
	dac ya
	sub yb
	xct sh1
	cma
	add xa
	dac xa
	lio ya
	dpy-i
m3b,	lac xb
	sub xc
	xct sh2
	add yb
	dac yb
	sub yc
	xct sh3
	cma
	add xb
	dac xb
	lio yb
	dpy-i
m3c,	lac xc
	sub xa
	xct sh4
	add yc
	dac yc
	sub ya
	xct sh5
	cma
	add xc
	dac xc
	lio yc
	dpy-i
	jmp m3a

gsh,	dap gsx
	cla
	rcl 3s
	add gsc
	dap .+1
	lac .
gsx,	jmp .

gsc,	gst
gst,	sar 1s
	sar 2s
	sar 3s
	sar 4s
	sar 5s
	sar 6s
	sar 7s
	sar 8s
	sar 9s	/ not used, but was in orig.

xa0,	dpy i 17770
xb0,	0
xc0,	and
ya0,	0
yb0,	ior
yc0,	0

xa,	0
xb,	0
xc,	0
ya,	0
yb,	0
yc,	0

sh0,	xx
sh1,	xx
sh2,	xx
sh3,	xx
sh4,	xx
sh5,	xx

msq,	lat
	sas msv
	sza i
	law 2345
	add 7
	dac 7
	rcl 9s
	xor 7
	dpy-i+300
	jmp msq
msv,	777777

start beg

